029-2,113,57,0|script|#alchemylab|400
{
    title "Alchemy Lab";
    mes "This is an alchemy lab.";
    mes "With it, you can combine reagents together to create powerful potions.";
    next;
    clear;
    mes "##BDrag & drop 2 items from your inventory. All items used will be lost.##b";
    requestitem @alchlab_items$[0], 2; // TODO: add a craft builtin (npc action 12)
    clear;
    if (@alchlab_items$[0] == "" || @alchlab_items$[1] == "")
        goto L_NoItems;

    mes "You are about to combine the following:";
    mes "     ["+ getitemlink(@alchlab_items$[0]) +"]";
    mes "     ["+ getitemlink(@alchlab_items$[1]) +"]";
    mes;
    mes "##BWarning! This will permanently destroy the selected items.##b";
    // can not use a scope variable to store item names because menu is a script terminator (destroys the scope)
    menu
        "Abort Mission.", L_Abort,
        "Proceed.", L_Proceed;

L_Proceed:
    // XXX: here we could make it wait a little, while the potion is boiling (and show an animation/particles/sound)
    if (countitem(@alchlab_items$[0]) < 1 || countitem(@alchlab_items$[1]) < 1) // this shouldn't happen, but just to be safe..
        goto L_NoItems;
    delitem @alchlab_items$[0], 1;
    delitem @alchlab_items$[1], 1;
    if (@alchlab_items$[0] == "BottleOfWater" || @alchlab_items$[1] == "BottleOfWater") // give back used bottles
        getitem "EmptyBottle", if_then_else(@alchlab_items$[0] == "BottleOfWater" && @alchlab_items$[1] == "BottleOfWater",2,1);

    set .@n, -3;
    goto L_CheckRecipes;

L_CheckRecipes:
    set .@n, .@n+3;
    if (.@n == get(.rsize, "#alchemylab"))
        goto L_Failed;
    if (!(get(.recipes$[.@n], "#alchemylab") == @alchlab_items$[0] && get(.recipes$[.@n+1], "#alchemylab") == @alchlab_items$[1]) &&
        !(get(.recipes$[.@n], "#alchemylab") == @alchlab_items$[1] && get(.recipes$[.@n+1], "#alchemylab") == @alchlab_items$[0]))
        goto L_CheckRecipes;

    getitem get(.recipes$[.@n+2], "#alchemylab"), 1; // XXX: here we could also make it fail sometimes depending on your expertise

    // XXX: here we could make it give profession/crafting exp
    clear;
    mes "You combined the following:";
    mes "     ["+ getitemlink(@alchlab_items$[0]) +"]";
    mes "     ["+ getitemlink(@alchlab_items$[1]) +"]";
    mes;
    mes "You obtained:";
    mes "     ["+ getitemlink(get(.recipes$[.@n+2], "#alchemylab")) +"]";
    close;

L_NoItems:
    mes "You must put exactly 2 items.";
    close;

L_Failed:
    mes "The potion bubbles violently and evaporates."; // XXX: here we could have random failure messages
    close;

L_Abort:
    close;

OnInit:
    void // the first alchemy lab doesn't need a puppet, it's already on a map
        puppet("027-2", 51, 100, strnpcinfo(0)+0, 400), // TODO: make a npc like npc 400 but with hoverCursor="action" => looks better for things like crafting
        puppet("001-2", 92,  76, strnpcinfo(0)+1, 400),
        puppet("001-2", 98,  76, strnpcinfo(0)+2, 400),
        puppet("001-2", 92,  89, strnpcinfo(0)+3, 400),
        puppet("001-2", 98,  89, strnpcinfo(0)+4, 400);

    setarray .recipes$[0],
        "DilutedConcentrationPot", "DarkConcentrationPotion", "ConcentrationPotion",
        "CactusDrink", "CactusDrink", "CactusPotion",
        "PinkPetal", "BottleOfWater", "ConcentrationPotion";
    set .rsize, getarraysize(.recipes$);
    end;
}
