-|script|flare-dart|32767
{
    if(call("magic_checks")) end;
    if (Sp < 10) end;
    set @level, getskilllv(.school);
    if (getskilllv(SKILL_MAGIC) < .level) end;
    if (@level <= 2 && countitem("SulphurPowder") >= 1) delitem "SulphurPowder", 1;
    elif (@level <= 2) end;
    set @_M_BLOCK, 1; // block casting, until the timer clears it
    addtimer 500, "Magic Timer::OnClear"; // set the new debuff
    callfunc "adjust_spellpower";
    set Sp, Sp - 10;
    misceffect FX_MAGIC_BLACK, strcharinfo(0);
    set @flar_damage, sqrt(@spellpower) * 5;
    set @flar_dmg_bonus, (BaseLevel/3) + 5;
    set .@delay, (((200 - Agi) * 1200) / 200);
    overrideattack (@spellpower/50)+3, .@delay, 4, ATTACK_ICON_GENERIC, 34, strnpcinfo(0)+"::OnAttack";
    callfunc "magic_exp";
    end;

OnAttack:
    misceffect FX_MAGIC_BLACK, strcharinfo(0);
    if (target(BL_ID, @target_id, 50) != 50) end; // 0x20 | 0x02 | 0x10
    void call("elt_damage", @flar_damage, @flar_dmg_bonus, ELT_WATER, ELT_FIRE, FX_MAGIC_BLACK);
    end;

OnInit:
    set .school, SKILL_MAGIC_WAR;
    set .invocation$, chr(MAGIC_SYMBOL) + "flar"; // used in npcs that refer to this spell
    void call("magic_register");
    set .level, 1;
    set .exp_gain, 1;
    end;
}
